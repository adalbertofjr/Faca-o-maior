apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        free {
            applicationId 'com.udacity.gradle.builditbigger.free'
        }
        paid {
            applicationId 'com.udacity.gradle.builditbigger.paid'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.google.code.findbugs:jsr305:2.0.1'
    compile 'com.google.api-client:google-api-client:1.22.0'
    compile 'com.google.http-client:google-http-client-android:1.22.0'
    freeCompile 'com.google.android.gms:play-services-ads:10.2.1'
    compile project(':jokeview')
    compile project(path: ':jokerbackend', configuration: 'android-endpoints')
}

task runAppEngine {
    dependsOn ':jokerbackend:appengineRun'
    evaluationDependsOn ':jokerbackend'
    project(":jokerbackend") {
        appengine {
            daemon true
        }
    }

    doLast {
        println "App engine iniciado..."
    }
}

task stopAppEngine {
    dependsOn ":jokerbackend:appengineStop"
    doLast {
        println "Encerrando app engine..."
    }
}

task runAndroidTest {
    dependsOn "runAppEngine"

    doFirst {
        println "Iniciando testes..."
    }

    dependsOn 'connectedAndroidTest'
    dependsOn ':jokeview:connectedAndroidTest'
    dependsOn ':javaJokesProvider:test'

    doLast {
        println "Encerrando testes..."
    }

    finalizedBy "stopAppEngine"
}

